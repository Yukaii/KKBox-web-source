# 	Mp3tag parser for kkbox website
#
# 	Src created by otacorn(ç‰ç±³)
#
# #################################

[Name]=KKbox
[BasedOn]=http://www.kkbox.com
[IndexUrl]=http://www.kkbox.com/tw/tc/search.php?search=album&word=%s
[AlbumUrl]=http://www.kkbox.com
[WordSeperator]=+
[IndexFormat]=%_url%|%Album%|%Artist%
[SearchBy]=%album%
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################

debug "on" "c:\\debug_kkbox1.htm"

findline "class=\"albums five\">"

do
	findline "li class=\"album\""
	findline "class=\"cover\""

	# Url
	findinline "href=\""
	sayuntil "\""
	say "|"

	# Album
	findinline "title=\""
	sayuntil "\""
	say "|"

	# Artist
	findline "item-h album-name" 2
	findline "<a href"
	findinline "title=\""
	sayuntil "\""
	saynewline

	findline "<li class=\""
	findinline "class=\""
while "album"


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
debug "on" "c:\\debug_kkbox2.htm"

# Album
outputto "Album"
findline "class=\"fa fa-angle-right\""
findline "span"
findinline "span>"
sayuntil "</span>"

# CoverURL
outputto "coverurl"
findline "<img src=\""
findinline "src=\""
sayuntil "\""


gotoline 1

# Artist
outputto "artist"
findline "<p class=\"tit-14\">"
findline "<a"
findinline ">"
sayuntil "</a"

# Year
outputto "Year"
findline "<p class=\"tit-16\">" 2
findinline ">"
saynextnumber

# Genre
outputto "Genre"
findline "<p class=\"tit-14\">"
findline "<p class=\"tit-16\">"
findinline ">"
sayuntil "</"


findline "song-list js-song-list"

do
	# Track
	outputto "track temp"
	findline "data-song_idx"
	findinline "data-song_idx=\""
	saynextnumber
	say "|"

	# Title
	outputto "title temp"
	findline "class=\"song-data"
	findline "<a href="
	findinline ">"
	sayuntil "</"
	say "|"

	# tracks ++
	outputto "tracks"
	say "|"

	findline "<li"
	findinline "<li "

while "class=\"clearfix\""

outputto "title"
sayoutput "title temp"

outputto "track"
sayoutput "track temp"

set "title temp"
set "track temp"
