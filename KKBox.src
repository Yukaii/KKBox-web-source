# 	Mp3tag parser for kkbox website
#
# 	Src created by otacorn(ç‰ç±³)
#
# #################################

[Name]=KKbox
[BasedOn]=http://www.kkbox.com
[IndexUrl]=http://www.kkbox.com/tw/tc/search.php?search=album&word=%s
[AlbumUrl]=http://www.kkbox.com
[WordSeperator]=+
[IndexFormat]=%_url%|%Album%|%Artist%
[SearchBy]=%album%
[Encoding]=url-utf-8


[ParserScriptIndex]=...
# ###################################################################
#					I  N  D  E  X
# ###################################################################

# debug "on" "c:\\debug_kkbox1.htm"

findline "class=\"albums five\">"

do
	findline "class=\"album\""
	findline "class=\"cover\""
	# findline "href=\""

	# Url
	findinline "href=\""
	sayuntil "\""
	say "|"

	# Album
	findline "title=\""
	findinline "title=\""
	sayuntil "\""
	say "|"

	# Artist
	findline "item-h album-name" 2
	findline "<a href"
	findinline "title=\""
	sayuntil "\""
	saynewline

	findline "class=\""
	findinline "class=\""
while "album"


[ParserScriptAlbum]=...
# ###################################################################
#					A  L  B  U  M
# ###################################################################
debug "on" "c:\\debug_kkbox2.htm"

# Album
outputto "Album"
findline "<h1 itemprop=\"name\">"
findinline "<h1 itemprop=\"name\">"
sayuntil "</h1>"

# sort of rewind Zzzz
gotoline 1

# CoverURL
outputto "coverurl"
findline "<div class=\"figure\">"
findline "<img src=\""
findinline "src=\""
sayuntil "\""

# lets go through table
findline "<dl class=\"data\""
findline "<dt>"

# Artist
outputto "artist"
findline "<a"
findinline ">" 2
sayuntil "</a"

# Genre
outputto "Genre"
findline "<dd>" 2
findinline "<dd>"
sayuntil "</dd>"

# Year
outputto "Year"
findline "<dd>" 2
findinline "<dd>"
saynextnumber


findline "class=\"song-list\""

do
	# Track
	outputto "track temp"
	findline "td class=\"song-track"
	findinline "song-track\">"
	# sayuntil "</td>"
	saynextnumber
	say "|"

	# Title
	outputto "title temp"
	findline "<a href="
	findinline ">"
	sayuntil "<em>"
	say "|"

	# tracks ++
	outputto "tracks"
	say "|"

	findline "</tr>"
	findinline "</tr>"

while "<tr id=\"songrow"

outputto "title"
sayoutput "title temp"

outputto "track"
sayoutput "track temp"

set "title temp"
set "track temp"
